input string 1113122113
> + > + > + > +++ > + > ++ > ++ > + > + > +++ > 
end of array contains counter 40
+++++ ++++  

> +++++ +++++  +++++ +++++  +++++ +++++  +++++ +++++  ++ . STAR
  ----- -----  ----- -----  ----- -----  ----- -----  -- <
  +++++ +++++  +++++ +++++  +++++ +++++  +++++ +++++  +++++ +++ . NUM
  ----- -----  ----- -----  ----- -----  ----- -----  ----- ---
> +++++ +++++ . NL 
  ----- ----- <

:__arr__ c
:        |
[
	:print nums
	> +++++ +++++  +++++ +++++  +++++ +++++  +++++ +++++  +++++ +++++  +++++ +++ . COLON
	  ----- -----  ----- -----  ----- -----  ----- -----  ----- -----  ----- --- <
	< [<] >> [ < 
	+++++ +++++  +++++ +++++ +++++ +++++  +++++ +++++  +++++ +++ . NUM 
	----- -----  ----- ----- ----- -----  ----- -----  ----- --- >> 
	]
	+++++ +++++ . NL 
	----- ----- <

	: x __1st__ c 0 1 y
	>> +       1
	<< [<] >   x
	[ [>] >> + <<< [<] > - ]   y:=x

	: |
	: 0 __1st__ l c 0
	> [>] <<    l
	[
		:copy x to y
		: 0 x __1st__ 0 __2nd__ 0 y
		[<] >   x
		[ 
			[>]     end 1st
			> [>]   lst 2nd
			> + <<  y
			[<]     beg 2nd
			< [<] > fst 1st
			- 
		]

		last of 2nd array
		> [>] > [>]

		:    |
		:n x 0 y t s
		:t = x my
		<  x [->+>>+<<<]  x to 0;t
		>  0 [-<+>]       0 to x
		>  y [->->+<<]    t my; y to s
		>> s [-<<+>>]     s to y

		:t is now 0 if x=y
		<[  if t\=0
			:most negative is (1 m 3 = n2)
			++ 
			:(0 n1 n2 1 2) now ge 0
			[-]
			:3 after last
			>>
		]
		:n x 0 y 0 0 0
		:if t        |
		:else    |


		:        |
		:n x 0 k 0
		<[-]   k:=0
		<< < + increment n
		:lst 1st
		[<] <<
	]

	:c 0 __2nd__ 0
	>  c 
	>> [>] + [<] <
	:c 0 __2nd__ d 0
	[ - >> [>] < + [<] < ]   d:=c
	>> [>] < --  decrement d
]

:print nums
> +++++ +++++  +++++ +++++  +++++ +++++  +++++ +++++  +++++ +++++  +++++ +++ . COLON
  ----- -----  ----- -----  ----- -----  ----- -----  ----- -----  ----- --- <
< [<] > [ 
+++++ +++++  +++++ +++++ +++++ +++++  +++++ +++++  +++++ +++ . NUM 
----- -----  ----- ----- ----- -----  ----- -----  ----- ---
> ]
+++++ +++++ . NL 
----- -----

:a b c __arr__ z 0 n
< [ z
	>> + n
	<<   z
	[<] > [-] > pop a
	[>] < 
]
> +++++ +++++  +++++ +++++ +++++ +++++  +++++ +++++  +++++ +++++  +++++ +++++  + . EQUAL
  ----- -----  ----- ----- ----- -----  ----- -----  ----- -----  ----- -----  - >


:n q T s
[ n
	[ n
		-
		>+>----- ---- q p1; T p1 m10 = T m9
		[<->+++++ +++++[->+<]] q m1; T p10; T to s
		>[-<+>] s to T
		<<< n
	]

	>>[-<<+>>] T to n
	<<
	+++++ +++++  +++++ +++++  +++++ +++++  +++++ +++++  +++++ +++ NUM

	> q
]

<[.<]
+++++ +++++ . NL
----- -----

